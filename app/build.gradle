plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Load local.properties for API keys
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    namespace 'com.example.snapshop'
    compileSdk 34
    ndkVersion "27.0.12077973"

    defaultConfig {
        applicationId "com.example.snapshop"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        // Inject API keys from local.properties into BuildConfig
        buildConfigField "String", "GOOGLE_VISION_API_KEY", "\"${localProperties.getProperty('GOOGLE_VISION_API_KEY', '')}\""
        buildConfigField "String", "MERCHANT_WALLET_ADDRESS", "\"${localProperties.getProperty('MERCHANT_WALLET_ADDRESS', '')}\""
        buildConfigField "String", "AMAZON_AFFILIATE_TAG", "\"${localProperties.getProperty('AMAZON_AFFILIATE_TAG', 'snapshop0f5-20')}\""

        // OpenRouter LLM Vision (replaces Google Vision API for product identification)
        buildConfigField "String", "OPENROUTER_API_KEY", "\"${localProperties.getProperty('OPENROUTER_API_KEY', '')}\""
        buildConfigField "String", "OPENROUTER_MODEL", "\"${localProperties.getProperty('OPENROUTER_MODEL', 'google/gemini-3-flash-preview')}\""

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-std=c++17"
            }
        }

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    signingConfigs {
        release {
            storeFile file('C:/Users/26375/Desktop/software/my table4/seeker')
            storePassword localProperties.getProperty('STORE_PASSWORD', '')
            keyAlias 'key0'
            keyPassword localProperties.getProperty('KEY_PASSWORD', '')
        }
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
            version '3.22.1'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jni/libs']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // CameraX
    def camerax_version = "1.3.1"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    // Solana Mobile Wallet Adapter (MWA) for Seeker wallet
    implementation 'com.solanamobile:mobile-wallet-adapter-clientlib-ktx:2.0.3'

    // Solana Web3 - Transaction building
    implementation 'com.solanamobile:web3-solana:0.2.5'

    // Solana RPC Client
    implementation 'com.solanamobile:rpc-core:0.2.7'

    // Ktor for network (required by RPC client)
    implementation 'io.ktor:ktor-client-android:2.3.7'
    implementation 'io.ktor:ktor-client-content-negotiation:2.3.7'
    implementation 'io.ktor:ktor-serialization-kotlinx-json:2.3.7'

    // Base58 encoding
    implementation 'io.github.funkatronics:multimult:0.2.3'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // Lifecycle for coroutines scope
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'

    // SnapShop new dependencies
    // Image loading (product images)
    implementation 'io.coil-kt:coil:2.5.0'

    // Chrome Custom Tabs (in-app browser with shared cookies)
    implementation 'androidx.browser:browser:1.7.0'

    // CardView (for home screen cards)
    implementation 'androidx.cardview:cardview:1.0.0'

    // RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
}
